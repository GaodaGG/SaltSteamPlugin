plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '2.0.21'
    id 'org.jetbrains.kotlin.kapt' version '2.0.21'
}

group = 'com.gg.SaltSteamPlugin'
version = '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenLocal()
    mavenCentral()
    google()
    maven {
        url = 'https://jitpack.io'
    }
}

dependencies {
    compileOnly 'net.jthink:jaudiotagger:3.0.1'
    compileOnly "com.github.Moriafly:spw-workshop-api:0.1.0-dev20"
    kapt "com.github.Moriafly:spw-workshop-api:0.1.0-dev20"

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

def pluginClass = "com.gg.SaltSteamPlugin.MainPlugin"
def pluginId = "com.gg.SaltSteamPlugin"
def pluginName = "Steam 丰富状态扩展"
def pluginVersion = "2.4.0"
def pluginProvider = "GaodaGG"
def pluginOpenSourceUrl = "https://github.com/GaodaGG/SaltSteamPlugin"
def pluginDescription = "通过 Steam API 丰富 Salt Player 的状态信息"

tasks.named("jar") {
    archiveFileName = "plugin-${pluginId}-${pluginVersion}.jar"

    manifest {
        attributes["Plugin-Class"] = pluginClass
        attributes["Plugin-Id"] = pluginId
        attributes["Plugin-Name"] = pluginName
        attributes["Plugin-Version"] = pluginVersion
        attributes["Plugin-Provider"] = pluginProvider
        attributes["Plugin-Description"] = pluginDescription
        attributes["Plugin-Open-Source-Url"] = pluginOpenSourceUrl
        attributes["Plugin-Has-Config"] = true
    }
}

tasks.register("plugin", Jar) {
    archiveFileName = "plugin-${pluginName}-${pluginVersion}.zip"
    archiveBaseName.set("plugin-" + pluginName + "-" + pluginVersion)

    into("classes") {
        with(tasks.named("jar").get())
    }
    dependsOn(configurations.runtimeClasspath)
    into("lib") {
        from({
            configurations.runtimeClasspath
                    .filter { it.name.endsWith("jar") }
        })
    }
    archiveExtension = 'zip'
}